buildscript {
  repositories {
    maven {
      url "https://maven.eveoh.nl/content/repositories/releases"
    }
  }

  dependencies {
    classpath "nl.eveoh:gradle-aspectj:1.6"
  }
}

plugins {
  id 'java'
  id 'eclipse'
  id 'war'
  id 'jetty'
  id 'maven'
  id 'jacoco'
  // id 'pmd'
  
}

project.ext {
  aspectjVersion = '1.8.6'
}

apply plugin: 'aspectj'
 


/*
 * Gets the version name from the latest Git tag of the form V-X.Y where X and Y are version numbers
 */
def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--match', 'V-*.*'
    standardOutput = stdout
  }
  def v = stdout.toString().trim() =~ /V-(\d+)\.(\d+)(?:-(\d+)-)?/
  return v[0][1] + "." + v[0][2] + "." + (v[0][3] == null ? 0 : v[0][3])
}


group = 'ch.kerbtier.dime2'
version = getVersionName()


sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  exclude
}

dependencies {
  compile('ch.kerbtier.webb:webb:0.1.+') {
    exclude group: 'org.apache.ant'
    exclude group: 'org.eclipse.jetty'
  }
  
  providedCompile "javax.servlet:servlet-api:2.5"
  
  compile 'ch.kerbtier.helene:helene:0.1.+'
  compile 'ch.kerbtier.amarillo:amarillo:0.1.+'
  compile 'org.pegdown:pegdown:1.5.0'
  runtime 'com.h2database:h2:1.4.188'
  
  aspectpath 'ch.kerbtier.esdi:esdi:0.1.+'
  
  testCompile 'org.testng:testng:6.9.6'
}

test {
  useTestNG()
  
  testLogging {
    exceptionFormat = 'full'
  }
  
  maxHeapSize = '8m'
}

jar {
  baseName = 'dime2'
  version =  version
  manifest {
    attributes 'Implementation-Title': 'dime2', 'Implementation-Version': version
  }
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}

httpPort = 8004

jettyRunWar {
  if(project.hasProperty('webbContext')) {
    contextPath = webbContext
  }
  reload = "manual"
}