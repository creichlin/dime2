

/*
 * Gets the version name from the latest Git tag of the form V-X.Y where X and Y are version numbers
 */
def getVersionName = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'describe', '--tags', '--match', 'V-*.*'
    standardOutput = stdout
  }
  def v = stdout.toString().trim() =~ /V-(\d+)\.(\d+)(?:-(\d+)-)?/
  return v[0][1] + "." + v[0][2] + "." + (v[0][3] == null ? 0 : v[0][3])
}

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.lesscss:lesscss:1.7.0.1.1"
  }
}

import org.lesscss.LessCompiler


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenLocal()
  mavenCentral()
}

group = 'ch.kerbtier.dime2'
version = getVersionName()

configurations {
  exclude
}

test {
  useTestNG()
  
  testLogging {
    exceptionFormat = 'full'
  }
  
  maxHeapSize = '8m'
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}

apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

dependencies {
  compile project('dime2-core')
}


task lessCompile << {
  def compiler = new org.lesscss.LessCompiler()
  
  File base = file('src/main/webapp')
  fileTree(dir: base, include: '**/*.css').each {File css ->
    def rel = base.toURI().relativize(css.toURI()).toString()
    compiler.compile(css, file("build/less/" + rel + ".precompiled"))
  }
}

war {
  dependsOn lessCompile
  
  from 'build/less' 
}


gretty {
  port = 8004
}

